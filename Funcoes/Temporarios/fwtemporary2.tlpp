#include 'protheus.ch'

/*/{Protheus.doc} U_ARQUIVO_TEMP
(Programa para exemplo sobre o funcionamento de arquivos temporarios)
@type user function
@author Dario Leandro
@since 13/11/2025
/*/
function U_ARQUIVO_TEMP_3

    Local oTempTable as object
    Local cAliasTemp as character
    Local cRealName  as character
    Local cArquivo   as character
    Local lArquivo   as logical
    
    rpcSetEnv('99','01')

        cAliasTemp  :="TRBSA1SA2"
        oTempTable  := fwTemporaryTable():new(cAliasTemp)
        oTempTable:setFields({{"ENTIDADE","C",3,0} ,{"CODIGO","C",6,0} , {"LOJA","C",2,0}, {"NOME","C",30,0}})
        oTempTable:addIndex("IND_1",{"CODIGO","LOJA","ENTIDADE"})
        oTempTable:create()

        cRealName   := oTempTable:getRealName()

        dbSelectArea('SA1')
        dbSetOrder(1)

        while .not. eof()
            
            (cAliasTemp)->(reclock(alias(),.T.), CODIGO := SA1->A1_COD, LOJA := SA1->A1_LOJA,  NOME := SA1->A1_NOME,ENTIDADE := SA1->(alias()),msunlock())
            dbSkip()

        Enddo

        dbSelectArea('SA2')
        dbSetOrder(1)

        while .not. eof()
            
            (cAliasTemp)->(reclock(alias(),.T.), CODIGO := SA2->A2_COD, LOJA := SA2->A2_LOJA,  NOME := SA2->A2_NOME,ENTIDADE := SA2->(alias()),msunlock())
            dbSkip()

        Enddo

        cTxt := "ENTIDADE;CODIGO;LOJA;NOME"

        (cAliasTemp)->(dbSetOrder(1),dbEval({|| cTxt += CRLF + ENTIDADE + ';' + CODIGO + ';' + LOJA + ';' + NOME}))

        cArquivo := "K:\listaEntidade.csv"
        lArquivo := memoWrite(cArquivo,cTxt)

        (cAliasTemp)->(dbCloseArea())
        oTempTable:delete()


    rpcClearEnv()

Return
